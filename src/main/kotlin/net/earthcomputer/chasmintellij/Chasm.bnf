{
    parserClass = "net.earthcomputer.chasmintellij.ChasmParser"

    extends = "com.intellij.extapi.psi.ASTWrapperPsiElement"

    psiClassPrefix = "Chasm"
    psiImplClassSuffix = "Impl"
    psiPackage = "net.earthcomputer.chasmintellij.psi"
    psiImplPackage = "net.earthcomputer.chasmintellij.psi.impl"

    elementTypeHolderClass = "net.earthcomputer.chasmintellij.psi.ChasmTypes"
    elementTypeClass = "net.earthcomputer.chasmintellij.psi.ChasmElementType"
    tokenTypeClass = "net.earthcomputer.chasmintellij.psi.ChasmTokenType"

    psiImplUtilClass = "net.earthcomputer.chasmintellij.psi.impl.ChasmPsiImplUtil"
}

chasmFile ::= SKIP? expression SKIP? <<eof>>

expression ::= lambdaExpression | ternaryPrecedence

lambdaExpression ::= identifier SKIP? LAMBDA SKIP? expression {
pin = 3
extends=expression
implements = "com.intellij.psi.PsiNameIdentifierOwner"
methods=[argumentElement="identifier" getArgumentName nameIdentifier="identifier" getName setName body="expression"]
}

private ternaryPrecedence ::= ternaryExpression | booleanOrPrecedence

ternaryExpression ::= booleanOrPrecedence SKIP? TERNARY SKIP? ternaryPrecedence SKIP? COLON SKIP? ternaryPrecedence {
pin = 3
extends=expression
methods=[condition="expression[0]" trueExpr="expression[1]" falseExpr="expression[2]"]
}

private booleanOrPrecedence ::= booleanAndPrecedence booleanOrExpression?

left booleanOrExpression ::= SKIP? BOOL_OR SKIP? booleanOrPrecedence {
pin = 2
extends=expression
implements="net.earthcomputer.chasmintellij.psi.ChasmBinaryExpression"
methods=[left="expression[0]" right="expression[1]"]
}

private booleanAndPrecedence ::= bitwiseOrPrecedence booleanAndExpression?

left booleanAndExpression ::= SKIP? BOOL_AND SKIP? booleanAndPrecedence {
pin = 2
extends=expression
implements="net.earthcomputer.chasmintellij.psi.ChasmBinaryExpression"
methods=[left="expression[0]" right="expression[1]"]
}

private bitwiseOrPrecedence ::= bitwiseXorPrecedence bitwiseOrExpression?

left bitwiseOrExpression ::= SKIP? BITWISE_OR SKIP? bitwiseOrPrecedence {
pin = 2
extends=expression
implements="net.earthcomputer.chasmintellij.psi.ChasmBinaryExpression"
methods=[left="expression[0]" right="expression[1]"]
}

private bitwiseXorPrecedence ::= bitwiseAndPrecedence bitwiseXorExpression?

left bitwiseXorExpression ::= SKIP? BITWISE_XOR SKIP? bitwiseXorPrecedence {
pin = 2
extends=expression
implements="net.earthcomputer.chasmintellij.psi.ChasmBinaryExpression"
methods=[left="expression[0]" right="expression[1]"]
}

private bitwiseAndPrecedence ::= equalityPrecedence bitwiseAndExpression?

left bitwiseAndExpression ::= SKIP? BITWISE_AND SKIP? bitwiseAndPrecedence {
pin = 2
extends=expression
implements="net.earthcomputer.chasmintellij.psi.ChasmBinaryExpression"
methods=[left="expression[0]" right="expression[1]"]
}

private equalityPrecedence ::= relationalPrecedence equalityExpression?

left equalityExpression ::= SKIP? (EQUAL | NOT_EQUAL) SKIP? equalityPrecedence {
pin = 2
extends=expression
implements="net.earthcomputer.chasmintellij.psi.ChasmBinaryExpression"
methods=[left="expression[0]" right="expression[1]"]
}

private relationalPrecedence ::= shiftPrecedence relationalExpression?

left relationalExpression ::= SKIP? (LESS_THAN | LESS_THAN_EQUAL | GREATER_THAN | GREATER_THAN_EQUAL) SKIP? relationalPrecedence {
pin = 2
extends=expression
implements="net.earthcomputer.chasmintellij.psi.ChasmBinaryExpression"
methods=[left="expression[0]" right="expression[1]"]
}

private shiftPrecedence ::= additivePrecedence shiftExpression?

left shiftExpression ::= SKIP? (SHIFT_LEFT | SHIFT_RIGHT | SHIFT_RIGHT_UNSIGNED) SKIP? shiftPrecedence {
pin = 2
extends=expression
implements="net.earthcomputer.chasmintellij.psi.ChasmBinaryExpression"
methods=[left="expression[0]" right="expression[1]"]
}

private additivePrecedence ::= multiplicativePrecedence additiveExpression?

left additiveExpression ::= SKIP? (PLUS | MINUS) SKIP? additivePrecedence {
pin = 2
extends=expression
implements="net.earthcomputer.chasmintellij.psi.ChasmBinaryExpression"
methods=[left="expression[0]" right="expression[1]"]
}

private multiplicativePrecedence ::= unaryPrecedence multiplicativeExpression?

left multiplicativeExpression ::= SKIP? (MULTIPLY | DIVIDE | MODULO) SKIP? multiplicativePrecedence {
pin = 2
extends=expression
implements="net.earthcomputer.chasmintellij.psi.ChasmBinaryExpression"
methods=[left="expression[0]" right="expression[1]"]
}

private unaryPrecedence ::= unaryExpression | argumentPrecedence

unaryExpression ::= (PLUS | MINUS | NOT | INVERT) SKIP? unaryPrecedence {
pin = 1
extends=expression
methods=[getOperator operand="expression"]
}

private argumentPrecedence ::= primaryExpression argumentExpression?

private argumentExpression ::= (callExpression | indexExpression | memberExpression)*

left callExpression ::= SKIP? LPAREN SKIP? bracketsBody SKIP? RPAREN {
pin = 2
extends=expression
methods=[function="expression[0]" argument="expression[1]"]
}

left indexExpression ::= SKIP? LBRACKET SKIP? bracketsBody SKIP? RBRACKET {
pin = 2
extends=expression
methods=[owner="expression[0]" index="expression[1]"]
}

left memberExpression ::= SKIP? DOT SKIP? identifier {
pin = 2
extends=expression
methods=[owner="expression[0]" memberNameElement="identifier" getMemberName getReference]
}

private primaryExpression ::= literalExpression | referenceExpression | parenthesesExpression | listExpression | mapExpression

literalExpression ::= NULL | BOOL | FLOAT | INTEGER | (QUOTE (UNESCAPED_STRING | ESCAPED_STRING)* QUOTE) | (SINGLE_QUOTE (UNESCAPED_STRING | ESCAPED_STRING) SINGLE_QUOTE) {
pin = 1
extends=expression
implements=["com.intellij.psi.ContributedReferenceHost"]
methods=[getReferences getValue]
}

referenceExpression ::= DOLLAR? identifier {
pin = 2
extends=expression
methods=[referenceElement="identifier" getReferenceName getReference isGlobal]
}

private bracketsBody ::= expression {
recoverWhile = bracketsRecover
}
private bracketsRecover ::= !(RPAREN | RBRACE | RBRACKET)

private listBracketsBody ::= expression {
recoverWhile = listBracketsRecover
}
private listBracketsRecover ::= !(RPAREN | RBRACE | RBRACKET | COMMA)

parenthesesExpression ::= LPAREN SKIP? bracketsBody SKIP? RPAREN {
pin = 1
extends=expression
}

listExpression ::= LBRACKET SKIP? (listBracketsBody (SKIP? COMMA SKIP? listBracketsBody)* SKIP? (COMMA SKIP?)?)? RBRACKET {
pin = 1
extends=expression
}

mapExpression ::= LBRACE SKIP? (mapEntry (SKIP? COMMA SKIP? mapEntry)* SKIP? (COMMA SKIP?)?)? RBRACE {
pin = 1
extends=expression
}

mapEntry ::= identifier SKIP? COLON SKIP? expression {
pin=1
implements = "com.intellij.psi.PsiNameIdentifierOwner"
methods = [keyElement="identifier" nameIdentifier="identifier" getKey getName setName value="expression"]
recoverWhile = mapEntryRecover
}
private mapEntryRecover ::= !(COMMA | RBRACE | RPAREN | RBRACKET)

identifier ::= IDENT
